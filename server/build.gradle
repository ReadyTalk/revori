buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.mapvine:gradle-cobertura-plugin:1.0'
  }
}

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'cobertura'

ext {
  mainClassName='com.readytalk.revori.server.SQLServer'
  serverPort='8017'
  serverHost='localhost'
}

repositories {
  mavenCentral()
}

dependencies {
  ext {
      findbugsVersion = '2.0.1'
      guavaVersion = '14.0-rc1'
      slf4jVersion = '1.7.2'
      logbackVersion = '1.0.9'
      junitVersion = '4.11'
      mockitoVersion = '1.9.5'
  }

  findbugs "com.google.code.findbugs:findbugs:${findbugsVersion}"
  
  testCompile (
  	["junit:junit:${junitVersion}"],
  	["org.mockito:mockito-all:${mockitoVersion}"],
  )

  compile (
    ["com.google.code.findbugs:jsr305:${findbugsVersion}"],
    ["com.google.code.findbugs:annotations:${findbugsVersion}"],
    ["com.google.guava:guava:${guavaVersion}"],
    ["org.slf4j:slf4j-api:${slf4jVersion}"]
  )

  runtime "ch.qos.logback:logback-classic:${logbackVersion}"
}

jar {
  baseName = 'revori-server'
}

cobertura {
  format = 'html'
  includes = ['**/*.java']
}

findbugs {
  ignoreFailures = true
}

test {
  //Here due to an issue with cobertura and jre7.
  jvmArgs "-XX:-UseSplitVerifier"
}

task start {
  dependsOn 'build'

  group = 'application'
  description = 'Start the Revori server.'

  doLast {
    ant.java(classname: mainClassName,
             classpath: runtimeClasspath.asPath,
             fork: true,
             spawn: true) {
      arg(value: serverHost)
      arg(value: serverPort)
    }
  }
}

task stop {
  group = 'application'
  description = 'Stop the Revori server.'

  doLast {
    ant.exec(executable: 'pkill') {
      arg(value: '-f')
      arg(value: mainClassName)
    } 
  }
}